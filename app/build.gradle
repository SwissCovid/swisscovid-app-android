/*
 * Copyright (c) 2020 Ubique Innovation AG <https://www.ubique.ch>
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * SPDX-License-Identifier: MPL-2.0
 */

plugins {
	id 'com.android.application'
	id "org.sonarqube" version "2.8"
}

apply from: 'backend_certs.gradle'

ext.readProperty = { paramName -> readPropertyWithDefault(paramName, null) }
ext.readPropertyWithDefault = { paramName, defaultValue ->
	if (project.hasProperty(paramName)) {
		return project.getProperties().get(paramName)
	} else {
		Properties properties = new Properties()
		if (project.rootProject.file('local.properties').exists()) {
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
		}
		if (properties.getProperty(paramName) != null) {
			return properties.getProperty(paramName)
		} else {
			return defaultValue
		}
	}
}

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"

	defaultConfig {
		applicationId "ch.admin.bag.dp3t"
		minSdkVersion 23
		targetSdkVersion 29
		versionCode 10080
		versionName "1.0.8"
		resConfigs "en", "fr", "de", "it", "pt", "es", "sq", "bs", "hr", "sr", "rm", "tr", "ti"

		buildConfigField "long", "BUILD_TIME", readPropertyWithDefault('buildTimestamp', System.currentTimeMillis()) + 'L'

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		testInstrumentationRunnerArguments clearPackageData: 'true'
	}

	flavorDimensions "version"

	productFlavors.whenObjectAdded { flavor ->
		// generate BuildConfig.IS_FLAVOR_### flags
		String flavorFieldName = "IS_FLAVOR_" + flavor.name.replaceAll("[^a-zA-Z0-9_]", "_").toUpperCase()
		defaultConfig.buildConfigField "boolean", flavorFieldName, "false"
		flavor.buildConfigField "boolean", flavorFieldName, "true"
	}

	productFlavors {
		dev {
			buildConfigField 'boolean', 'DEV_HISTORY', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-d.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-d.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.dev.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.dev.BUCKET_PUBLIC_KEY}\""
			applicationIdSuffix '.dev'
		}
		tescht {
			buildConfigField 'boolean', 'DEV_HISTORY', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-t.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-t.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-t.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-t.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.test.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.test.BUCKET_PUBLIC_KEY}\""
			applicationIdSuffix '.test'
		}
		abnahme {
			buildConfigField 'boolean', 'DEV_HISTORY', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-a.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-a.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.abnahme.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.abnahme.BUCKET_PUBLIC_KEY}\""
			applicationIdSuffix '.abnahme'
		}
		prod {
			buildConfigField 'boolean', 'DEV_HISTORY', 'false'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.prod.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.prod.BUCKET_PUBLIC_KEY}\""
		}
	}

	signingConfigs {
		release {
			storeFile file(readPropertyWithDefault('keystoreFile', 'testKeystore'))
			storePassword readProperty('keystorePassword')
			keyAlias 'keyAlias'
			keyPassword readProperty('keyAliasPassword')
		}
	}

	buildTypes {
		release {
			shrinkResources true
			setMinifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
}


sonarqube {
	properties {
		property "sonar.projectKey", "DP-3T_dp3t-app-android-ch"
		property "sonar.organization", "dp-3t"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.projectName", "dp3t-app-android-ch"
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])

	def dp3t_sdk_version = '1.0.3'
	devImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version-calibration"
	teschtImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"
	abnahmeImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"
	prodImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"

	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
	implementation 'androidx.fragment:fragment:1.2.4'
	implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
	implementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
	implementation 'androidx.viewpager2:viewpager2:1.0.0'
	implementation 'androidx.security:security-crypto:1.0.0-rc01'
	implementation 'androidx.work:work-runtime:2.3.4'

	implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
	implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

	implementation 'com.google.android.material:material:1.1.0'
	implementation 'com.google.android.gms:play-services-base:17.2.1'

	implementation 'com.squareup.retrofit2:retrofit:2.8.1'
	implementation 'com.squareup.retrofit2:converter-gson:2.8.1'

	androidTestImplementation 'androidx.test.ext:junit:1.1.1'
	androidTestImplementation 'androidx.test:runner:1.2.0'
	androidTestImplementation 'androidx.test:core:1.2.0'
	androidTestImplementation 'androidx.test:rules:1.2.0'
	androidTestImplementation 'androidx.work:work-testing:2.3.4'
}
