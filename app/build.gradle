/*
 * Created by Ubique Innovation AG
 * https://www.ubique.ch
 * Copyright (c) 2020. All rights reserved.
 */

plugins {
	id 'com.android.application'
}

apply from: 'backend_certs.gradle'

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"

	defaultConfig {
		applicationId "ch.admin.bag.dp3t"
		minSdkVersion 23
		targetSdkVersion 29
		versionCode 1000
		versionName "1.0"
		resConfigs "en", "fr", "de", "it"

		buildConfigField "long", "BUILD_TIME", System.currentTimeMillis() + "L"
	}

	flavorDimensions "version"
	productFlavors {
		dev {
			buildConfigField 'boolean', 'IS_DEV', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-d.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-d.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.dev.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.dev.BUCKET_PUBLIC_KEY}\""
		}
		abnahme {
			buildConfigField 'boolean', 'IS_DEV', 'false'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-a.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-a.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.abnahme.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.abnahme.BUCKET_PUBLIC_KEY}\""
		}
		prod {
			buildConfigField 'boolean', 'IS_DEV', 'false'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1.bfs.admin.ch/"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.prod.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.prod.BUCKET_PUBLIC_KEY}\""
		}
	}

	sourceSets {
		abnahme {
			java.srcDirs += 'src/prod/java'
		}
	}

	signingConfigs {
		release {
			storeFile file('testKeystore')
			storePassword '123456'
			keyAlias 'keyAlias'
			keyPassword '123456'
		}
	}
	buildTypes {
		release {
			shrinkResources true
			setMinifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	def dp3t_sdk_version = '0.2.2'
	devImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version-calibration"
	abnahmeImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"
	prodImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"

	implementation 'androidx.appcompat:appcompat:1.1.0'
	implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
	implementation 'androidx.fragment:fragment:1.2.4'
	implementation 'androidx.lifecycle:lifecycle-viewmodel:2.2.0'
	implementation 'androidx.lifecycle:lifecycle-livedata:2.2.0'
	implementation 'androidx.viewpager2:viewpager2:1.0.0'
	implementation 'androidx.security:security-crypto:1.0.0-rc01'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
	implementation 'androidx.work:work-runtime:2.3.4'

	implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
	implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

	implementation 'com.google.android.material:material:1.1.0'

	implementation 'com.squareup.retrofit2:retrofit:2.6.2'
	implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
}
