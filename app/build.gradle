/*
 * Copyright (c) 2020 Ubique Innovation AG <https://www.ubique.ch>
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at https://mozilla.org/MPL/2.0/.
 *
 * SPDX-License-Identifier: MPL-2.0
 */

plugins {
	id 'com.android.application'
	id 'kotlin-android'
	id 'org.sonarqube' version '2.8'

}

apply from: 'backend_certs.gradle'

android {
	compileSdkVersion 30

	defaultConfig {
		applicationId "ch.admin.bag.dp3t"
		minSdkVersion 23
		targetSdkVersion 30
		versionCode 20001
		versionName "2.0.0"
		resConfigs "en", "fr", "de", "it", "pt", "es", "sq", "bs", "hr", "sr", "rm", "tr", "ti"

		buildConfigField "long", "BUILD_TIME", readPropertyWithDefault('buildTimestamp', System.currentTimeMillis()) + 'L'

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		testInstrumentationRunnerArguments clearPackageData: 'true'
	}

	flavorDimensions "version"

	productFlavors.whenObjectAdded { flavor ->
		// generate BuildConfig.IS_FLAVOR_### flags
		String flavorFieldName = "IS_FLAVOR_" + flavor.name.replaceAll("[^a-zA-Z0-9_]", "_").toUpperCase()
		defaultConfig.buildConfigField "boolean", flavorFieldName, "false"
		flavor.buildConfigField "boolean", flavorFieldName, "true"
	}

	productFlavors {
		dev {
			buildConfigField 'boolean', 'DEV_HISTORY', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-d.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-d.bfs.admin.ch/"'
			buildConfigField 'String', 'STATS_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField 'String', 'PUBLISHED_CROWDNOTIFIER_KEYS_BASE_URL', '"https://www.pt-d.bfs.admin.ch/"'
			buildConfigField "String", "ENTRY_QR_CODE_HOST", '"qr-d.swisscovid.ch"'
			buildConfigField "String", "QR_MASTER_PUBLIC_KEY_BASE_64", '"lW5voTRVR-jgYMiWLd04hjvyyFQG7QOyBLw0D7XbASlqlg0AviQMqgjbABZk9PcCip27szrqFyv_1YtKZE8eyzt7vtN4qKfJdWrItLRzRtjb83piN3cDt_yNo7siohQV"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.dev.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.dev.BUCKET_PUBLIC_KEY}\""
			applicationIdSuffix '.dev'
			manifestPlaceholders = [qrCodeHostName: "qr-d.swisscovid.ch"]
		}
		abnahme {
			buildConfigField 'boolean', 'DEV_HISTORY', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service-a.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1-a.bfs.admin.ch/"'
			buildConfigField 'String', 'STATS_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField 'String', 'PUBLISHED_CROWDNOTIFIER_KEYS_BASE_URL', '"https://www.pt-a.bfs.admin.ch/"'
			buildConfigField "String", "ENTRY_QR_CODE_HOST", '"qr-a.swisscovid.ch"'
			buildConfigField "String", "QR_MASTER_PUBLIC_KEY_BASE_64", '"RHk4TO95YVnihWuCSVnbQ3ow-AcO-BXZ9caWeO_GP7o-vEiowJXTkKSUAf77n4oAmzshDvFVLkQFsA72JfBcaC8QSIT9owk83tnDuwpUuDfFgiBq_KXqcA2UcxaInmQS"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.abnahme.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.abnahme.BUCKET_PUBLIC_KEY}\""
			applicationIdSuffix '.abnahme'
			manifestPlaceholders = [qrCodeHostName: "qr-a.swisscovid.ch"]
		}
		prod {
			buildConfigField 'boolean', 'DEV_HISTORY', 'false'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1.bfs.admin.ch/"'
			buildConfigField 'String', 'STATS_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'PUBLISHED_CROWDNOTIFIER_KEYS_BASE_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField "String", "ENTRY_QR_CODE_HOST", '"qr.swisscovid.ch"'
			buildConfigField "String", "QR_MASTER_PUBLIC_KEY_BASE_64", '"mAofNDGOKcmVJt4fo7kWruvFRee2g0irAfIdjH6EFyd6vHY_uTb6ZXOf9eFVzTUEiZLDLL-Q2w8kZkPscWyXi3X59zuqeI25qloOF-sowHo1_-HiH7Z_4COOA030mm8J"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.prod.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.prod.BUCKET_PUBLIC_KEY}\""
			manifestPlaceholders = [qrCodeHostName: "qr.swisscovid.ch"]
		}
		log {
			buildConfigField 'boolean', 'DEV_HISTORY', 'true'
			buildConfigField 'String', 'AUTH_CODE_URL', '"https://codegen-service.bag.admin.ch/"'
			buildConfigField 'String', 'CONFIG_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'BUCKET_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'REPORT_URL', '"https://www.pt1.bfs.admin.ch/"'
			buildConfigField 'String', 'STATS_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField 'String', 'PUBLISHED_CROWDNOTIFIER_KEYS_BASE_URL', '"https://www.pt.bfs.admin.ch/"'
			buildConfigField "String", "ENTRY_QR_CODE_HOST", '"qr.swisscovid.ch"'
			buildConfigField "String", "QR_MASTER_PUBLIC_KEY_BASE_64", '"mAofNDGOKcmVJt4fo7kWruvFRee2g0irAfIdjH6EFyd6vHY_uTb6ZXOf9eFVzTUEiZLDLL-Q2w8kZkPscWyXi3X59zuqeI25qloOF-sowHo1_-HiH7Z_4COOA030mm8J"'
			buildConfigField 'String', 'CONFIG_CERTIFICATE', "\"${project.backend_certs.prod.CONFIG_CERTIFICATE}\""
			buildConfigField 'String', 'BUCKET_PUBLIC_KEY', "\"${project.backend_certs.prod.BUCKET_PUBLIC_KEY}\""
			manifestPlaceholders = [qrCodeHostName: "qr.swisscovid.ch"]
		}
	}

	signingConfigs {
		release {
			storeFile file(readPropertyWithDefault('keystoreFile', 'testKeystore'))
			storePassword readProperty('keystorePassword')
			keyAlias readPropertyWithDefault('keyAlias', 'keyAlias')
			keyPassword readProperty('keyAliasPassword')
		}
	}

	buildTypes {
		release {
			shrinkResources false
			setMinifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			signingConfig signingConfigs.release
		}
	}

	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		coreLibraryDesugaringEnabled true
	}

	kotlinOptions {
		jvmTarget = "1.8"
	}

	buildFeatures {
		viewBinding true
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "DP-3T_dp3t-app-android-ch"
		property "sonar.organization", "dp-3t"
		property "sonar.host.url", "https://sonarcloud.io"
		property "sonar.projectName", "dp3t-app-android-ch"
	}
}


dependencies {

	implementation project(":common")

	implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
	implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0'

	def dp3t_sdk_version = '2.3.0-dev-8'
	devImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version-calibration"
	abnahmeImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"
	prodImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"
	logImplementation "org.dpppt:dp3t-sdk-android:$dp3t_sdk_version"

	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
	implementation 'androidx.fragment:fragment-ktx:1.3.3'
	implementation "androidx.activity:activity-ktx:1.2.3"
	implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
	implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
	implementation 'androidx.viewpager2:viewpager2:1.0.0'
	implementation 'androidx.security:security-crypto:1.0.0-rc03'
	implementation 'androidx.work:work-runtime-ktx:2.5.0'
	implementation "androidx.datastore:datastore:1.0.0-beta01"

	implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
	implementation 'io.reactivex.rxjava3:rxjava:3.0.0'

	implementation 'com.google.android.material:material:1.2.1'
	implementation 'com.google.android.gms:play-services-base:17.5.0'
	implementation 'com.google.protobuf:protobuf-lite:3.0.1'
	implementation('com.squareup.retrofit2:converter-protobuf:2.9.0') { transitive = false }
	implementation 'com.squareup.retrofit2:retrofit:2.9.0'
	implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

	// QR code scanning
	def camerax_version = "1.0.0-rc04"
	implementation "androidx.camera:camera-camera2:${camerax_version}"
	implementation "androidx.camera:camera-lifecycle:${camerax_version}"
	implementation "androidx.camera:camera-view:1.0.0-alpha23"
	//DO NOT UPDATE ZXING TO 3.4.0! (3.4.0 is not compatible with API Level 23 or lower)
	implementation "com.google.zxing:core:3.3.0"

	implementation 'androidx.biometric:biometric:1.1.0'

	implementation 'io.github.ShawnLin013:number-picker:2.4.13'

	coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

	androidTestImplementation 'androidx.test.ext:junit:1.1.2'
	androidTestImplementation 'androidx.test:runner:1.3.0'
	androidTestImplementation 'androidx.test:core:1.3.0'
	androidTestImplementation 'androidx.test:rules:1.3.0'
	androidTestImplementation 'androidx.work:work-testing:2.5.0'
	androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.14.7'

}
